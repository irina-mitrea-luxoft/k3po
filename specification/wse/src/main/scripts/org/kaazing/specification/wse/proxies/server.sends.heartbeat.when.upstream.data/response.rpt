property upstreamId ${wse:uniqueId()}
property downstreamId ${wse:uniqueId()}
property upstream ${wse:append("http://localhost:8080/path/", upstreamId)}
property downstream ${wse:append("http://localhost:8080/path/", downstreamId)}

accept http://localhost:8080/path/;e/cb?query
accepted
connected

read method "POST"
read version "HTTP/1.1"
read header "X-WebSocket-Version" "wseb-1.0"
read header "X-Sequence-No" /(?<sequence>[0-9]{1,3})/
read closed

write status "201" "Created" 
write version "HTTP/1.1"
write header "Content-Type" "text/plain;charset=utf-8"
write header content-length

write ${upstream} "\n"
write ${downstream} "\n"
write close

###########################################################
# Downstream
accept ${downstream}
accepted
connected

read parameter ".kkt" "10"
read method "GET"
read version "HTTP/1.1"
read header "X-Sequence-No" ${wse:asString(sequence + 1)}
read closed

write status "200" "OK"
write version "HTTP/1.1"
write header "Content-Type" "application/octet-stream"
write header "Connection" "close"
write flush

write await SEND_TWO
write [0x01 0x30 0x30 0xFF]
write [0x01 0x30 0x30 0xFF]
write await CLOSE_ONE_RECEIVED
write [0x01 0x30 0x32 0xFF]
write [0x01 0x30 0x31 0xFF]
write close
###########################################################
# Upstream
accept ${upstream}
accepted
connected

read method "POST"
read version "HTTP/1.1"
read header "Content-Type" "application/octet-stream"
read header "X-Sequence-No" ${wse:asString(sequence + 1)}

# Read random data from client @SEND_ONE
read [0x82 0x82] ([0..4] :readMask)
read option mask ${readMask}
read [0x11 0x11]
read option mask [0x00 0x00 0x00 0x00]

# Read random data from client @ SEND_TWO
read [0x82 0x82] ([0..4] :readMask)
read option mask ${writeMask}
read [0x11 0x11]
read option mask [0x00 0x00 0x00 0x00]

read [0x01 0x30 0x32 0xFF]
read [0x01 0x30 0x31 0xFF]
read notify CLOSE_ONE_RECEIVED
read closed

write status "200" "OK"
write version "HTTP/1.1"
write header content-length
write close
###########################################################
